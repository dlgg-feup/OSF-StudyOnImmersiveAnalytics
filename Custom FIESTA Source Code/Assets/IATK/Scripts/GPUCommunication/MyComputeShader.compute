// Each #kernel tells which function to compile; you can have many kernels
#pragma kernel CSMain
#pragma kernel ComputeBrushedIndicesArray
#pragma kernel ResetBrushTexture
#pragma kernel ComputeNearestDistances
#include "UnityCG.cginc"

// Create a RenderTexture with enableRandomWrite flag and set it
// with cs.SetTexture
RWTexture2D<float4> Result;
float4x4 W_Matrix;
float rand;

float _size;

//*******************
// RANGE FILTERING
//*******************

float _MinX;
float _MaxX;
float _MinY;
float _MaxY;
float _MinZ;
float _MaxZ;

// ********************
// Normalisation ranges
// ********************

float _MinNormX;
float _MaxNormX;
float _MinNormY;
float _MaxNormY;
float _MinNormZ;
float _MaxNormZ;

float3 pointer1;
float3 pointer2;

float height;
float width;
float depth;

float RadiusSphere;
float RadiusInspector;

//struct VecIndexPair
//{
//    float3 pos;
//    int index;
//};

int BrushMode;
int SelectionMode;

float4 brushColor;
float4 sharedBrushColor;
bool shareBrushing;

int previousIndex;
int highlightedIndex;

RWStructuredBuffer<float3> dataBuffer;
RWStructuredBuffer<int> brushedIndices;
StructuredBuffer<float> filteredIndices;
RWStructuredBuffer<float> nearestDistances;

//brush circle
bool brush2DInCircle(float3 center, float radius, float4 testPoint)
{
	float4 projectedCenter = mul(W_Matrix, float4(center, 1.0));

	return sqrt(pow(testPoint.x - projectedCenter.x, 2) + pow(testPoint.y - projectedCenter.y, 2)) < radius;
}

//brush sphere
bool brushSphere(float3 center, float radius, float3 testPoint)
{
	float3 displacementToCenter = testPoint - center;
	float radiusSqr = radius * radius;
	bool intersects = dot(displacementToCenter, displacementToCenter) < radiusSqr;

	return intersects;
}

bool brushCube(float3 TFL, float3 BBR, float3 testPoint)
{
	return (testPoint.x >= TFL.x && testPoint.x <= BBR.x
		&&  testPoint.y <= TFL.y && testPoint.y >= BBR.y
		&&  testPoint.z >= TFL.z && testPoint.z <= BBR.z);
}

bool brushSquare(float3 tl, float3 tr, float3 bl, float3 br, float4 testPoint)
{
	float4 tlp = mul(W_Matrix, float4(tl, 1.0));
	float4 trp = mul(W_Matrix, float4(tr, 1.0));
	float4 blp = mul(W_Matrix, float4(bl, 1.0));
	float4 brp = mul(W_Matrix, float4(br, 1.0));

	return (testPoint.x >= tlp.x && testPoint.x <= trp.x
		&&  testPoint.y <= tlp.y && testPoint.x >= blp.y);
}

/*
Adapts the brush
0 : sphere
1 : cube
2 : square

*/
bool Brush(int brushType, float3 filteredPosition)
{
	switch (brushType)
	{
		//brush sphere
	case 0:
		return brushSphere(float3(pointer1.x, pointer1.y, pointer1.z), RadiusSphere, filteredPosition);
		//brush cube
	case 1:
		float3 TL = float3(pointer1.x, pointer1.y, pointer1.z);
		float3 BR = float3(pointer2.x, pointer2.y, pointer2.z);

		if (TL.x > BR.x || TL.y < BR.y || TL.z > BR.z)
		{
			TL = BR;
			BR = float3(pointer1.x, pointer1.y, pointer1.z);
		}

		return brushCube(TL, BR, filteredPosition);
		// brush square
		//case 2: retun brushSquare(pointer1x, pointer1y,pointer2x,pointer2y,filteredPosition);
	default: return false;
	}
}

bool IsEqual(float4 value1, float4 value2, float tolerance) {
	if (abs(value1.x - value2.x) < tolerance
		&& abs(value1.y - value2.y) < tolerance
		&& abs(value1.z - value2.z) < tolerance
		&& abs(value1.w - value2.w) < tolerance) {
		return true;
	}
	else {
		return false;
	}
}

bool IsEqual(float value1, float value2, float tolerance) {
	if (abs(value1 - value2) < tolerance) {
		return true;
	}
	else {
		return false;
	}
}

// TODO
// brush sphere

//brush rectangle

//brush cube

//texturebrush

// brush histogram
//*********************************
// helper functions
//*********************************

float normaliseValue(float value, float i0, float i1, float j0, float j1)
{
	float L = (j0 - j1) / (i0 - i1);
	return (j0 - (L * i0) + (L * value));
}

[numthreads(32, 32, 1)]
void CSMain(uint3 id : SV_DispatchThreadID)
{
	// 1 calculate the brushing for the current pixel

	int idx = id.x + id.y * _size;
	//vertex shader stage

	// Only test if the point is not filtered
	if (!filteredIndices[idx]) {
		// DOES NOT WORK WITH NEGATIVE WIDTH/HEIGHT/DEPTH

		float3 filteredPosition = float3(
			normaliseValue(dataBuffer[idx].x, _MinNormX, _MaxNormX, 0, width),
			normaliseValue(dataBuffer[idx].y, _MinNormY, _MaxNormY, 0, height),
			normaliseValue(dataBuffer[idx].z, _MinNormZ, _MaxNormZ, 0, depth));

		//filters out dots 
		if (filteredPosition.x >= _MinX &&
			filteredPosition.x <= _MaxX * width &&
			filteredPosition.y >= _MinY &&
			filteredPosition.y <= _MaxY * height &&
			filteredPosition.z >= _MinZ &&
			filteredPosition.z <= _MaxZ * depth
			)
		{
			if (Brush(BrushMode, filteredPosition))
			{
				switch (SelectionMode) {
					// free selection
					case 0:
					// additive selection
					case 1:
						if (!shareBrushing) {
							if (Result[id.xy].w == 0) {  // only allow the private brush if the point is not already brushed
								Result[id.xy] = brushColor;
							}
						}
						else {
							Result[id.xy] = sharedBrushColor;  // shared brush can override others
						}
						break;
					// subtractive selection
					case 2:
						if (!shareBrushing) {
							if (IsEqual(Result[id.xy], brushColor, 0.01)) {  // private brush can only deselect private brushed points
								Result[id.xy] = float4(0.0, 0.0, 0.0, 0.0);
							}
						}
						else {
							Result[id.xy] = float4(0.0, 0.0, 0.0, 0.0);  // shared brush can override others
						}
						break;
				}
			}
			else
			{
				if (SelectionMode == 0)
					Result[id.xy] = float4(0.0, 0.0, 0.0, 0.0); // not brushed
			}
		}
		else
		{
			Result[id.xy] = float4(0.0, 0.0, 0.0, 0.0); // not brushed
		}
	}
}

[numthreads(32, 1, 1)]
void ComputeBrushedIndicesArray(uint3 id : SV_DispatchThreadID)
{
	int x = id.x % _size;
	int y = floor(id.x / _size);
	float2 pos = float2(x, y);

	if (Result[pos].w > 0.0)
		brushedIndices[id.x] = 1;
	else
		brushedIndices[id.x] = -1;
}

[numthreads(32, 32, 1)]
void ResetBrushTexture(uint3 id : SV_DispatchThreadID) {

	int idx = id.x + id.y * _size;

	Result[id.xy] = float4(0.0, 0.0, 0.0, 0.0);
}

[numthreads(32, 1, 1)]
void ComputeNearestDistances(uint3 id: SV_DispatchThreadID) {

	int idx = id.x;

	// Only test if the point is not filtered
	if (!filteredIndices[idx]) {
		// DOES NOT WORK WITH NEGATIVE WIDTH/HEIGHT/DEPTH

		float3 filteredPosition = float3(
			normaliseValue(dataBuffer[idx].x, _MinNormX, _MaxNormX, 0, width),
			normaliseValue(dataBuffer[idx].y, _MinNormY, _MaxNormY, 0, height),
			normaliseValue(dataBuffer[idx].z, _MinNormZ, _MaxNormZ, 0, depth));

		//filters out dots 
		if (filteredPosition.x >= _MinX &&
			filteredPosition.x <= _MaxX * width &&
			filteredPosition.y >= _MinY &&
			filteredPosition.y <= _MaxY * height &&
			filteredPosition.z >= _MinZ &&
			filteredPosition.z <= _MaxZ * depth
			)
		{
			float3 displacementToCenter = filteredPosition - float3(pointer1.x, pointer1.y, pointer1.z);
			float radiusSqr = RadiusInspector * RadiusInspector;
			float magnitudeSqr = dot(displacementToCenter, displacementToCenter);
			if (magnitudeSqr < radiusSqr) {
				nearestDistances[id.x] = sqrt(magnitudeSqr);
			}
			else {
				nearestDistances[id.x] = 9999;
			}
		}
		else
		{
			nearestDistances[id.x] = 9999;
		}
	}
	else {
		nearestDistances[id.x] = 9999;
	}
}